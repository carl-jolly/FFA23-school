
        //////////////////////////////////////////////////////////////////////////////
        // Input file for single bunch tracking                                     //
        //////////////////////////////////////////////////////////////////////////////
        Title,string="FETS-FFA test simulation using OPAL code";
        Option, ASCIIDUMP=TRUE;
        Option, ENABLEHDF5=FALSE;
        OPTION, PSDUMPFREQ=100000;
        Option, VERSION=10900;

        Option, ECHO=FALSE;

        
        Option, SPTDUMPFREQ=1;
        ////////// CONSTANTS ////////////////////////////////////
        REAL DEGREE=PI/180.;
        REAL MM=1000.;
        REAL C_LIGHT=0.3; // m/ns

        ////////// MODULES ///////////////////////
        BOOL DO_MAGNET_FIELD_MAPS=False;
        
        BOOL DO_RF=False;

        ////////// RING  ///////////////
        REAL R0=4.0;

                REAL E0=3;

        REAL P_MASS=938.272;
        REAL P_CHARGE=1.0;
        REAL P0=((E0+P_MASS)^2-P_MASS^2)^0.5;
        REAL N_CELLS=16;
        REAL RMIN=R0-1.0;
        REAL RMAX=R0+1.0;
        REAL FFA_CELL_LENGTH=2*PI/N_CELLS;
        REAL BFREQ=1;
        REAL NUM_TURNS =50;
        
        ////////// TRACKING ///////////////
        REAL STEP_SIZE=0.01; // m
        REAL BEAM_PHI_INIT=0.0;

        //////// MAIN MAGNETS ////////////////
        REAL CIRCUMFERENCE=8*PI;
        REAL BF=-0.34400191316643397;
        REAL BD=0.26356465577360527;
        REAL K_VAL=7.578216859923307;

        REAL LAMBDA=0.12;
        REAL SPIRAL_ANGLE=PI/4;
        REAL ENGE_0=-1.3;
        REAL ENGE_1=3.42;
        REAL ENGE_2=-1.20;
        REAL ENGE_3=0.23;

        REAL CELL_LENGTH = (2*PI/N_CELLS)*R0;
        REAL AZIMUTHAL_EXTENT=CELL_LENGTH;

        REAL FD_OFFSET = 0.0;

        REAL F_START = (9 - 2.75)*(PI/180)*R0;
        REAL F_CENTRE_LENGTH = (4.5*PI/180)*R0;
        REAL F_END = F_START+F_CENTRE_LENGTH;

        REAL D_START = (2.25)*(PI/180)*R0;
        REAL D_CENTRE_LENGTH = (2.25*PI/180)*R0;
        REAL D_END = D_START+D_CENTRE_LENGTH+(7.25)*(PI/180)*R0;

        ringdef: RINGDEFINITION, HARMONIC_NUMBER=1, LAT_RINIT=4.0, LAT_PHIINIT=1,
                LAT_THETAINIT=0.0, BEAM_PHIINIT=0.0, BEAM_PRINIT=0.0,
                BEAM_RINIT=0.0, SYMMETRY=1.0, RFFREQ=1, IS_CLOSED=true;

        IF (DO_MAGNET_FIELD_MAPS) {

            DUMPFIELDS, X_START=-5.0, X_STEPS=708, DX=0.100/4.*2./3., Y_START=-5.0, Y_STEPS=708, DY=0.100/4.*2./3., Z_START=0.001, Z_STEPS=1, DZ=0.100, FILE_NAME="FieldMapXY.dat";
            DUMPEMFIELDS, COORDINATE_SYSTEM=CYLINDRICAL, R_START=R0-0.1*R0, R_STEPS=225, DR=0.005, PHI_START=0., PHI_STEPS=1075, DPHI=0.001, Z_START=0, Z_STEPS=1, DZ=0.100, T_START=0, T_STEPS=1, DT=1., FILE_NAME="FieldMapRPHI.dat";

        }

        BUILD_PROBE(NAME, ANGLE, DANGLE): MACRO {
            NAME: PROBE, xstart=RMIN*1000*cos(ANGLE),  xend=RMAX*1000*cos(ANGLE),  ystart=RMIN*1000*sin(ANGLE),  yend=RMAX*1000*sin(ANGLE);
        }


        REAL THIS_PROBE_PHI=0;
        BUILD_PROBE(Probe1, THIS_PROBE_PHI, 0);
        THIS_PROBE_PHI = EVAL(THIS_PROBE_PHI+FFA_CELL_LENGTH);
        BUILD_PROBE(Probe2, THIS_PROBE_PHI, 0);
        THIS_PROBE_PHI = EVAL(THIS_PROBE_PHI+FFA_CELL_LENGTH);
        BUILD_PROBE(Probe3, THIS_PROBE_PHI, 0);
        THIS_PROBE_PHI = EVAL(THIS_PROBE_PHI+FFA_CELL_LENGTH);
        BUILD_PROBE(Probe4, THIS_PROBE_PHI, 0);
        THIS_PROBE_PHI = EVAL(THIS_PROBE_PHI+FFA_CELL_LENGTH);
        BUILD_PROBE(Probe5, THIS_PROBE_PHI, 0);
        THIS_PROBE_PHI = EVAL(THIS_PROBE_PHI+FFA_CELL_LENGTH);
        BUILD_PROBE(Probe6, THIS_PROBE_PHI, 0);
        THIS_PROBE_PHI = EVAL(THIS_PROBE_PHI+FFA_CELL_LENGTH);
        BUILD_PROBE(Probe7, THIS_PROBE_PHI, 0);
        THIS_PROBE_PHI = EVAL(THIS_PROBE_PHI+FFA_CELL_LENGTH);
        BUILD_PROBE(Probe8, THIS_PROBE_PHI, 0);
        THIS_PROBE_PHI = EVAL(THIS_PROBE_PHI+FFA_CELL_LENGTH);
        BUILD_PROBE(Probe9, THIS_PROBE_PHI, 0);
        THIS_PROBE_PHI = EVAL(THIS_PROBE_PHI+FFA_CELL_LENGTH);
        BUILD_PROBE(Probe10, THIS_PROBE_PHI, 0);
        THIS_PROBE_PHI = EVAL(THIS_PROBE_PHI+FFA_CELL_LENGTH);
        BUILD_PROBE(Probe11, THIS_PROBE_PHI, 0);
        THIS_PROBE_PHI = EVAL(THIS_PROBE_PHI+FFA_CELL_LENGTH);
        BUILD_PROBE(Probe12, THIS_PROBE_PHI, 0);
        THIS_PROBE_PHI = EVAL(THIS_PROBE_PHI+FFA_CELL_LENGTH);
        BUILD_PROBE(Probe13, THIS_PROBE_PHI, 0);
        THIS_PROBE_PHI = EVAL(THIS_PROBE_PHI+FFA_CELL_LENGTH);
        BUILD_PROBE(Probe14, THIS_PROBE_PHI, 0);
        THIS_PROBE_PHI = EVAL(THIS_PROBE_PHI+FFA_CELL_LENGTH);
        BUILD_PROBE(Probe15, THIS_PROBE_PHI, 0);
        THIS_PROBE_PHI = EVAL(THIS_PROBE_PHI+FFA_CELL_LENGTH);
        BUILD_PROBE(Probe16, THIS_PROBE_PHI, 0);
        THIS_PROBE_PHI = EVAL(THIS_PROBE_PHI+FFA_CELL_LENGTH);

        probes: Line = (
            Probe1, Probe2, Probe3, Probe4,
            Probe5, Probe6, Probe7, Probe8,
            Probe9, Probe10, Probe11, Probe12,
            Probe13, Probe14, Probe15, Probe16
        );


        magnet_f_end: ASYMMETRIC_ENGE,
                    X0_START=F_CENTRE_LENGTH/2, X0_END=F_CENTRE_LENGTH/2,
                    LAMBDA_START=LAMBDA, LAMBDA_END=LAMBDA,
                    COEFFICIENTS_START={ENGE_0, ENGE_1, ENGE_2, ENGE_3},
                    COEFFICIENTS_END={ENGE_0, ENGE_1, ENGE_2, ENGE_3};

        f_magnet: ScalingFFAMagnet, B0=BF,
                                    R0=R0,
                                    FIELD_INDEX=K_VAL,
                                    TAN_DELTA=tan(SPIRAL_ANGLE),
                                    MAX_Y_POWER=4,
                                    END_FIELD_MODEL=magnet_f_end,
                                    RADIAL_NEG_EXTENT=0.8,
                                    RADIAL_POS_EXTENT=1.2,
                                    MAGNET_START=F_START,
                                    MAGNET_END=F_END + FD_OFFSET, 
                                    HEIGHT=1,
                                    AZIMUTHAL_EXTENT=AZIMUTHAL_EXTENT;

        magnet_d_end: ASYMMETRIC_ENGE,
                    X0_START=D_CENTRE_LENGTH/2, X0_END=D_CENTRE_LENGTH/2,
                    LAMBDA_START=LAMBDA, LAMBDA_END=LAMBDA,
                    COEFFICIENTS_START={ENGE_0, ENGE_1, ENGE_2, ENGE_3},
                    COEFFICIENTS_END={ENGE_0, ENGE_1, ENGE_2, ENGE_3};

        d_magnet: ScalingFFAMagnet, B0=BD,
                                    R0=R0,
                                    FIELD_INDEX=K_VAL,
                                    TAN_DELTA=tan(SPIRAL_ANGLE),
                                    MAX_Y_POWER=4,
                                    END_FIELD_MODEL=magnet_d_end,
                                    RADIAL_NEG_EXTENT=0.8,
                                    RADIAL_POS_EXTENT=1.2,
                                    MAGNET_START=D_START,
                                    MAGNET_END=D_END,
                                    HEIGHT=1,
                                    AZIMUTHAL_EXTENT=AZIMUTHAL_EXTENT;


        ////////////////////////// RF CAVITY ///////////////////////////

        REAL FFA_RF_VOLTAGE=10; // [kV] total field in the FFA
        REAL FFA_RF_PHASE=1.90861500533; // [rad] RF phase
        REAL FFA_RF_FREQ_0=0.936565887561; // [MHz]
        REAL FFA_RF_FREQ_1=2.07316363698e-06; // [MHz/ns^1]
        REAL FFA_RF_FREQ_2=-4.82372209641e-13; // [MHz/ns^2]
        REAL FFA_RF_FREQ_3=0.0; // [MHz/ns^3]
        REAL MILLIMETRE = 1;

        rf_frequency: POLYNOMIAL_TIME_DEPENDENCE, P0=FFA_RF_FREQ_0, P1=FFA_RF_FREQ_1, P2=FFA_RF_FREQ_2, P3=FFA_RF_FREQ_3;
        rf_amplitude: POLYNOMIAL_TIME_DEPENDENCE, P0=FFA_RF_VOLTAGE/10; // V*1e-1 MV/m, 10 mm long cavity => V kV total
        rf_phase: POLYNOMIAL_TIME_DEPENDENCE, P0=FFA_RF_PHASE;
        cavity_drift: LOCAL_CARTESIAN_OFFSET, end_position_x=-0.010*MILLIMETRE, end_position_y=0.*MILLIMETRE, end_normal_x=1.*MILLIMETRE, end_normal_y=0.*MILLIMETRE; // go back to start of cavity
        rf_cavity: VARIABLE_RF_CAVITY, PHASE_MODEL="RF_PHASE", AMPLITUDE_MODEL="RF_AMPLITUDE", FREQUENCY_MODEL="RF_FREQUENCY", L=0.010*MILLIMETRE, HEIGHT=1.*MILLIMETRE, WIDTH=3.*MILLIMETRE;

        /////////////////////////// SIMULATION PARAMS //////////////////

        cell: Line = (f_magnet, d_magnet);
        l1: Line = (ringdef, probes,
                    cell,cell,cell,cell,cell,cell,cell,cell,cell,cell,cell,cell,cell,cell,cell,cell);

        IF (DO_RF) {
                cavity: Line = (cavity_drift, rf_cavity);
                cell: Line = (f_magnet, d_magnet);
                l1: Line = (ringdef, probes,
                        cell,cell,cell,cell,cell,cell,cell,cell,cell,cell,cell,cell,cell,cell,cell,cell,
                         cavity);
        } 


        REAL B_FREQ=1; // 1 MHz
        REAL SPEED=C_LIGHT*(P0/(E0+P_MASS));
        REAL STEP_SIZE_NS=STEP_SIZE/SPEED;
        REAL STEPS_PER_TURN=1000/STEP_SIZE_NS/B_FREQ; // (this is really steps per B_FREQ)

        Dist1: DISTRIBUTION, TYPE=fromfile, FNAME="perturbed_coords_3MeV.dat", INPUTMOUNITS=NONE;

        Fs1:FIELDSOLVER, FSTYPE=None, MX=8, MY=8, MT=8,
                PARFFTX=true, PARFFTY=true, PARFFTT=false,
                BCFFTX=open, BCFFTY=open, BCFFTT=open, BBOXINCR=2;

        beam1: BEAM, PARTICLE=PROTON, pc=P0*1e-3, NPART=1, BCURRENT=0, CHARGE=P_CHARGE, mass=P_MASS*1e-3, BFREQ=BFREQ;TRACK, LINE=l1, BEAM=beam1, MAXSTEPS=2850*NUM_TURNS, STEPSPERTURN=STEPS_PER_TURN;
        //RUN, METHOD="CYCLOTRON-T", BEAM=beam1, FIELDSOLVER=Fs1, DISTRIBUTION=Dist1, BOUNDARYGEOMETRY=RecPipe;
        RUN, METHOD="CYCLOTRON-T", BEAM=beam1, FIELDSOLVER=Fs1, DISTRIBUTION=Dist1;
        ENDTRACK;
        STOP;


        